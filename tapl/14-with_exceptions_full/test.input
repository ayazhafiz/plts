"hello";
unit;

timesfloat 2.0 3.14159;

succ (succ 0);
succ (pred 0);
pred (succ 0);

{x=true, y=false}; 
{x=true, y=false}.x;
{true, false}; 
{true, false}.1; 

lambda x:<a:Bool,b:Bool>. x;
lambda x:A. x;

lambda x:Nat. succ x;
(lambda x:Nat. succ (succ x)) (succ 0); 

s: Bool;
((lambda x: Bool->Bool.
   (lambda y: Bool. (x)(y))) (lambda z: Bool. s)) (true);

y = inert[F];
(lambda x: F. x)(y);

T = Nat->Nat;
lambda f:T. lambda x:Nat. f (f x);

let x=true in x;

plus = letrec plus: Nat->Nat->Nat =
  lambda m:Nat.
  lambda n:Nat.
    if iszero m then n else succ (plus (pred m) n)
  in plus;

times = letrec times: Nat -> Nat -> Nat =
  lambda m: Nat.
  lambda n: Nat.
    if iszero m then 0 else plus n (times (pred m) n)
  in times;

plus 8 5;
times 8 5;

Cat = {noise: String};
Dog = {noise: String};
Animal = <dog: Dog, cat: Cat>;
let myAnimal = (<cat = {noise="meow"}> as Animal) in
case myAnimal of
    <cat=kitty> ==> kitty.noise
  | <dog=puppy> ==> puppy.noise;

/* let diverge = (lambda _:Unit. fix (lambda x: Nat. x)) in diverge unit; */

numeric = letrec ieio: { iseven: Nat->Bool, isodd: Nat->Bool } =
  { iseven = lambda x: Nat.
      if (iszero x) then true
      else ieio.isodd (pred x),

    isodd = lambda x: Nat.
      if (iszero x) then false
      else ieio.iseven (pred x) }
  in ieio;

numeric.iseven 182;
numeric.isodd 183;

nil[Nat];
isnil (cons "a" (cons "b" nil[String]));
isnil (tail (cons "b" nil[String]));
let lst = (cons "a" (cons "b" nil[String]))
  in {hd=(head lst), tl=(tail lst)};

let a = 1.0 in let b = 2.0 in plusfloat a b;

FloatMap = Float -> Float;
FloatList = [Float];

mapFloat = letrec recur: FloatMap -> FloatList -> FloatList =
  lambda map: FloatMap.
  lambda lst: FloatList.
    if (isnil lst) then nil[Float]
    else cons (map (head lst)) (recur map (tail lst))
  in recur;

/* reduceFloat f(accumulator, current) -> Float, initial, list */
reduceFloat = letrec reduce: (Float -> Float -> Float) -> Float -> FloatList -> Float =
  lambda acc: Float -> Float -> Float.
  lambda init: Float.
  lambda lst: FloatList.
    if (isnil lst) then init
    else acc (reduce acc init (tail lst)) (head lst)
  in reduce;

"Sum of double([4, 3, 2, 1]) = Sum([8, 6, 4, 2]) =";

let double = mapFloat (lambda f: Float. timesfloat f 2.0) in
let sum = reduceFloat (lambda acc: Float. lambda cur: Float. plusfloat acc cur) 0.0 in
  sum (double (cons 4.0 (cons 3.0 (cons 2.0 (cons 1.0 nil[Float])))));

let a = ref 5 in (a := 1; !a);

/* Recursive factorial implemented with reference cells (!)
   The only thing is we do need a times function; for that just use the one we
   made previously. */

cell_fact = ref (lambda n: Nat. 0);
cell_fact := (lambda n: Nat. if iszero n then 1 else times n ((!cell_fact)(pred n)));
fact = !cell_fact;

fact 6;

/* Recursive fibonacci implemented with reference cells (!) */

cell_fib = ref (lambda n: Nat. 0);
cell_fib := (
  lambda n: Nat.
    if iszero n then 0
    else if iszero (pred n) then n
    else plus ((!cell_fib) (pred n)) ((!cell_fib) (pred (pred n)))
);
fib = !cell_fib;

fib 8;

/* Errors */
try (if iszero (fact 3) then "WRONGPATH" else error) except "yes, got out!";
try (if iszero 0 then "yes, got in!" else error) except "WRONGPATH";
